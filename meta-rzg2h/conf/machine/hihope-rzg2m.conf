#@TYPE: Machine
#@NAME: hihope-rzg2m machine
#@DESCRIPTION: Hoperun Technology HiHope RZ/G2M platform (hihope-rzg2m)

SOC_FAMILY = "r8a774a1"

DEFAULTTUNE ?= "cortexa57-cortexa53"
TUNE_CONF ?= "conf/machine/include/tune-cortexa57-cortexa53.inc"
require conf/machine/include/${SOC_FAMILY}.inc
require ${TUNE_CONF}
require conf/machine/include/cip-core.inc
require conf/machine/include/rzg2h-common.inc

# Configuration for kernel
PREFERRED_PROVIDER_virtual/kernel = "linux-renesas"
# Device tree for RZG2M
KERNEL_DEVICETREE = " \
    ${@oe.utils.ifelse("${ECC_FULL}" == "2"," "," \
    renesas/r8a774a1-hihope-rzg2m.dtb \
    renesas/r8a774a1-hihope-rzg2m-ex.dtb \
    renesas/r8a774a1-hihope-rzg2m-ex-idk-1110wr.dtb \
    renesas/r8a774a1-hihope-rzg2m-ex-mipi-2.1.dtb \
    renesas/r8a774a1-hihope-rzg2m-ex-mipi-2.4.dtb \
    renesas/r8a774a1-hihope-rzg2m-rev2.dtb \
    renesas/r8a774a1-hihope-rzg2m-rev2-ex.dtb \
    renesas/r8a774a1-hihope-rzg2m-rev2-ex-idk-1110wr.dtb \
    renesas/r8a774a1-hihope-rzg2m-rev2-ex-mipi-2.1.dtb \
    renesas/r8a774a1-hihope-rzg2m-rev2-ex-mipi-2.4.dtb \
    ")} \
    renesas/r8a774a3-hihope-rzg2m.dtb \
    renesas/r8a774a3-hihope-rzg2m-ex.dtb \
    renesas/r8a774a3-hihope-rzg2m-ex-idk-1110wr.dtb \
    renesas/r8a774a3-hihope-rzg2m-ex-mipi-2.1.dtb \
    renesas/r8a774a3-hihope-rzg2m-ex-mipi-2.4.dtb \
"

# G2M u-boot configure
UBOOT_CONFIG ??= "hihope-rzg2m"
UBOOT_CONFIG[hihope-rzg2m] = "hihope_rzg2_defconfig"

# flash writer
EXTRA_IMAGEDEPENDS += " flash-writer"

IMAGE_BOOT_FILES = " \
	bootparam_sa0.bin \
	bl2-${MACHINE}.bin \
	cert_header_sa6.bin \
	bl31-${MACHINE}.bin \
	tee-${MACHINE}.bin \
	u-boot-${MACHINE}.bin \
	AArch64_Flash_writer_SCIF_DUMMY_CERT_E6300400_hihope.mot \
"
WIC_INPUT_DEPENDS = "trusted-firmware-a:do_deploy u-boot:do_deploy flash-writer:do_deploy"

# Add variable to Build Configuration in build log
BUILDCFG_VARS_append = " SOC_FAMILY"

# Command using to build kernel-module-gles
HOSTTOOLS += "sync"
